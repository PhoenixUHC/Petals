plugins {
    id 'groovy'
    id 'java-library'
    id 'maven-publish'

    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'io.github.petals'
version = '0.1'

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
}

subprojects {
    def p = delegate.name

    if (p.endsWith 'api') {
        apply plugin: 'java-library'
        apply plugin: 'maven-publish'

        java {
            withSourcesJar()
            withJavadocJar()
        }

        publishing {
            publications {
                maven(MavenPublication) {
                    groupId = rootProject.group
                    version = rootProject.version
                    artifactId = p

                    from components.java
                }
            }
        }
    } else {
        apply plugin: 'groovy'
        apply plugin: 'com.github.johnrengelman.shadow'

        dependencies {
            implementation project(":${p.replace('impl', 'api')}")
        }
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(8))
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

